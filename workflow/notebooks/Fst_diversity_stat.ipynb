{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# $F_{ST}$ and diversity permutation tests"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#activate\n",
    "using Pkg\n",
    "Pkg.activate(\"../../.\")\n",
    "\n",
    "#import\n",
    "using NextGenSeqUtils, StatsBase, CSV, DataFrames\n",
    "\n",
    "#name parsers\n",
    "particip_func = x->String(split(x,'_')[2])\n",
    "tissue_func = x->String(split(x, '_')[3])\n",
    "\n",
    "#distances\n",
    "hamming(v1::String,v2::String) = sum(collect(v1) .!= collect(v2))\n",
    "\n",
    "function mean_pairwise_dist(g; dist_func = hamming)\n",
    "   l=length(g)\n",
    "   tot = 0.0\n",
    "   for i in 1:l\n",
    "       for j in i+1:l\n",
    "           tot+= dist_func(g[i],g[j])\n",
    "       end\n",
    "   end\n",
    "   return tot/((l*(l-1))/2)\n",
    "end\n",
    "\n",
    "function mean_pairwise_dist(g1,g2; dist_func = hamming)\n",
    "   l1,l2=length(g1),length(g2)\n",
    "   tot = 0.0\n",
    "   for i in 1:l1\n",
    "       for j in 1:l2\n",
    "           tot+= dist_func(g1[i],g2[j])\n",
    "       end\n",
    "   end\n",
    "   return tot/(l1*l2)\n",
    "end\n",
    "\n",
    "#null for permutation tests (stat_func should accept grouped sequences as Array{String,1})\n",
    "function null_distr(g1, g2, stat_func; reps = 10000)\n",
    "   l1,l2 = length(g1),length(g2)\n",
    "   merged = vcat(g1,g2)\n",
    "   stats = zeros(reps)\n",
    "   for i in 1:reps\n",
    "       scrambled = sample(merged,length(merged),replace = false)\n",
    "       newg1,newg2 = scrambled[1:l1],scrambled[l1+1:end]\n",
    "       stats[i] = stat_func(newg1,newg2)\n",
    "   end\n",
    "   return stats\n",
    "end\n",
    "\n",
    "#for FSTtest\n",
    "function FSTstat_weighted(g1, g2; dist_func = hamming)\n",
    "   within = (length(g1)*mean_pairwise_dist(g1; dist_func = dist_func) + length(g2)*mean_pairwise_dist(g2; dist_func = dist_func)) / (length(g1) + length(g2))\n",
    "   between = mean_pairwise_dist(g1,g2; dist_func = dist_func)\n",
    "   return (between - within) / between\n",
    "end\n",
    "\n",
    "function FSTtest_weighted(soqs, group_inds, dist_func; reps = 10000)\n",
    "   g1, g2 = soqs[group_inds[1]], soqs[group_inds[2]]\n",
    "   realstat = FSTstat_weighted(g1, g2; dist_func = dist_func)\n",
    "   return (sum((null_distr(g1, g2, FSTstat_weighted; reps = reps) .>= realstat))/reps, realstat)\n",
    "end\n",
    "\n",
    "#for two-tailed diversity test\n",
    "function div_test_stat(g1, g2; dist_func = hamming)\n",
    "    first = mean_pairwise_dist(g1; dist_func = dist_func);\n",
    "    second = mean_pairwise_dist(g2; dist_func = dist_func);\n",
    "    return abs(second-first)\n",
    "end\n",
    "\n",
    "function DivTest2sided(soqs, group_inds, dist_func; reps = 10000)\n",
    "    g1, g2 = soqs[group_inds[1]], soqs[group_inds[2]]\n",
    "    realstat = div_test_stat(g1, g2; dist_func = dist_func)\n",
    "    return (sum((null_distr(g1, g2, div_test_stat; reps = reps) .>= realstat))/reps, realstat)\n",
    "end\n",
    "\n",
    "#for grouping\n",
    "function get_group_inds(arr,func)\n",
    "    groupings = [func(i) for i in arr]\n",
    "    groups = union(groupings)\n",
    "    groupinds = [findall(groupings.==i) for i in groups]\n",
    "    return groupinds\n",
    "end\n",
    "\n",
    "function get_group_pair(arr, func)\n",
    "    groupings = [func(i) for i in arr]\n",
    "    groups = union(groupings)\n",
    "    grouppair = [i => findall(groupings.==i) for i in groups]\n",
    "    return grouppair\n",
    "end\n",
    "\n",
    "function get_comparisons(groups)\n",
    "    c = []\n",
    "    for i in 1:length(groups)-1\n",
    "        for j in i:length(groups)\n",
    "            if i != j\n",
    "                push!(c, (i,j))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return c\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#diversity analysis by tissue/participant...\n",
    "seqnames, seqs = read_fasta_with_names(\"../../results/sequences.aln\");\n",
    "participant = particip_func.(seqnames);\n",
    "\n",
    "res = []\n",
    "@time begin\n",
    "for p in unique(participant)\n",
    "    sel_seqs = seqs[participant .== p]\n",
    "    sel_names = seqnames[participant .== p]\n",
    "    group_pair = get_group_pair(sel_names, tissue_func);\n",
    "    \n",
    "    all_group_inds = getindex.(group_pair, 2)\n",
    "    all_group_ids = getindex.(group_pair, 1)\n",
    "    all_group_sizes = length.(all_group_inds)\n",
    "    \n",
    "    if length(all_group_inds) == 1\n",
    "        #skip if only one tissue... \n",
    "        push!(res, (p, all_group_ids[1], all_group_sizes[1],missing, missing, missing, missing))\n",
    "    else\n",
    "        #compare across tissues... \n",
    "        comparisons = get_comparisons(all_group_inds)\n",
    "        for c in comparisons\n",
    "            group_sizes = (all_group_sizes[c[1]], all_group_sizes[c[2]])\n",
    "            group_ids = (all_group_ids[c[1]], all_group_ids[c[2]])\n",
    "            group_inds = (all_group_inds[c[1]], all_group_inds[c[2]])\n",
    "            if sum(group_sizes .== 1) > 0\n",
    "                #skip if only one sequence for selected tissues...\n",
    "                push!(res, (p, all_group_ids[c[1]], group_sizes[1], all_group_ids[c[2]], group_sizes[2], missing, missing))\n",
    "            else\n",
    "                #run test... \n",
    "                pval,stat = DivTest2sided(sel_seqs, group_inds, hamming; reps = 10000)\n",
    "                push!(res, (p, all_group_ids[c[1]], group_sizes[1], all_group_ids[c[2]], group_sizes[2], pval, stat))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end\n",
    "end\n",
    "\n",
    "div_results = DataFrame(res)\n",
    "rename!(div_results, [:participant, :tissue_1, :n_tissue_1, :tissue_2, :n_tissue_2, :pval, :stat])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "CSV.write(\"diversity.tsv\", div_results; delim = '\\t')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#FST analysis by tissue/participant...\n",
    "seqnames, seqs = read_fasta_with_names(\"../../results/sequences.aln\");\n",
    "participant = particip_func.(seqnames);\n",
    "\n",
    "res = []\n",
    "@time begin\n",
    "for p in unique(participant)\n",
    "    sel_seqs = seqs[participant .== p]\n",
    "    sel_names = seqnames[participant .== p]\n",
    "    group_pair = get_group_pair(sel_names, tissue_func);\n",
    "    \n",
    "    all_group_inds = getindex.(group_pair, 2)\n",
    "    all_group_ids = getindex.(group_pair, 1)\n",
    "    all_group_sizes = length.(all_group_inds)\n",
    "    \n",
    "    if length(all_group_inds) == 1\n",
    "        push!(res, (p, all_group_ids[1], all_group_sizes[1],missing, missing, missing, missing))\n",
    "    else\n",
    "        #compare across tissues... \n",
    "        comparisons = get_comparisons(all_group_inds)\n",
    "        for c in comparisons\n",
    "            group_sizes = (all_group_sizes[c[1]], all_group_sizes[c[2]])\n",
    "            group_ids = (all_group_ids[c[1]], all_group_ids[c[2]])\n",
    "            group_inds = (all_group_inds[c[1]], all_group_inds[c[2]])\n",
    "                \n",
    "            if sum(group_sizes .== 1) > 0\n",
    "                #skip if only one sequence for selected tissues...\n",
    "                push!(res, (p, all_group_ids[c[1]], group_sizes[1], all_group_ids[c[2]], group_sizes[2], missing, missing))\n",
    "            else\n",
    "                #run test...\n",
    "                pval,stat = FSTtest_weighted(sel_seqs,group_inds,hamming; reps = 10000)\n",
    "                push!(res, (p, all_group_ids[c[1]], group_sizes[1], all_group_ids[c[2]], group_sizes[2], pval, stat))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end\n",
    "end\n",
    "\n",
    "fst_results = DataFrame(res)\n",
    "rename!(fst_results, [:participant, :tissue_1, :n_tissue_1, :tissue_2, :n_tissue_2, :pval, :stat])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "CSV.write(\"FST.tsv\", fst_results; delim = '\\t')"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
